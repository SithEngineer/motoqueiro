apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

def keystorePropertiesFile = rootProject.file("../keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
  signingConfigs {
    config {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion
  defaultConfig {
    applicationId "io.github.sithengineer.motoqueiro"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.compileSdkVersion
    versionCode 2
    versionName "0.2"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    //multiDexEnabled true

    buildConfigField "int", "LIMIT", "100"
    buildConfigField "int", "CACHE_TIME", "432000" // 5days

    buildConfigField "String", "WS_URL", "\"http://192.168.1.68:5000/api/\""
    buildConfigField "String", "MY_SECRET_KEY", "\"MY_SECRET_KEY\""
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      testProguardFiles getDefaultProguardFile('proguard-android.txt'),
          'proguardTest-rules.pro'
      applicationIdSuffix '.debug'
      versionNameSuffix '-debug'
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      testProguardFiles getDefaultProguardFile('proguard-android.txt'),
          'proguardTest-rules.pro'
      buildConfigField "String", "MY_SECRET_KEY", keystoreProperties['dataPassword']
      buildConfigField "String", "WS_URL", "\"http://motoqueiro.herokuapp.com/api/\""
      signingConfig signingConfigs.config
    }
  }
  /*
  productFlavors {
    mock {
      applicationIdSuffix = ".mock"
    }
    prod {}
  }

  // Remove mockRelease as it's not needed.
  android.variantFilter { variant ->
    if (variant.buildType.name.equals('release') && variant.getFlavors().
        get(0).name.
        equals('mock')) {
      variant.setIgnore(true);
    }
  }
  */
  packagingOptions {
    exclude 'META-INF/rxjava.properties'
  }

  applicationVariants.all { variant ->
    if (variant.getBuildType().isMinifyEnabled()) {
      variant.assemble.doLast {
        copy {
          from variant.mappingFile
          into "${rootDir}/proguardTools"
          rename { String fileName -> "mapping-${variant.name}.txt"
          }
        }
      }
    }
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

  implementation fileTree(include: ['*.jar'], dir: 'libs')
  androidTestImplementation(
      "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion",
      {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
      })
  testImplementation "junit:junit:$rootProject.junitVersion"
  implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
  implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
  implementation "com.android.support:design:$rootProject.supportLibraryVersion"
  implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"

  //  implementation("com.android.support:multidex:$rootProject.multiDexVersion") {
  //    exclude group: "com.android.support", module: "support-annotations"
  //  }
  implementation "com.google.android.gms:play-services-location:$rootProject.playServicesVersion"

  //
  // reactive extensions
  //
  implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"
  implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
  implementation "io.reactivex.rxjava2:rxkotlin:$rootProject.rxJavaVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rootProject.rxBindingVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rootProject.rxBindingVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rootProject.rxBindingVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rootProject.rxBindingVersion"

  //
  // dependency injection
  //
  implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
  kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"

  //
  // JSON (de)serialization
  //
  implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

  //
  // retrofit for REST API calls
  //
  implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
  implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
  implementation "com.squareup.retrofit2:converter-scalars:$rootProject.retrofitVersion"

  //
  // logging
  //
  implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"

  //
  // backported proper date classes
  //
  implementation "com.jakewharton.threetenabp:threetenabp:$rootProject.threeTenAbpVersion"

  //
  // room database
  //
  implementation "android.arch.persistence.room:runtime:$rootProject.roomVersion"
  kapt "android.arch.persistence.room:compiler:$rootProject.roomVersion"
  implementation "android.arch.persistence.room:rxjava2:$rootProject.roomVersion"
  testImplementation "android.arch.persistence.room:testing:$rootProject.roomVersion"
}
repositories {
  mavenCentral()
}
